/** 
* Welcome to the "Pig dice game"
* It is a 1-person dice game against the computer. Utilizing the random generator to simulate a rolling dice. The rules of the game are:
* Each turn, a player repeatedly rolls a die with count 1 through 6, by pressing "r", until either a "1" is rolled 
* or the player decides to "hold", by pressing "h":
* If the player rolls a 1, they score nothing and it becomes the next player's turn.
* If the player rolls any other number, it is added to their turn total and the player's turn continues.
* If a player chooses to "hold", their turn total is added to their score, and it becomes the next player's turn.
* The first player to score 100 or more points wins.
*/

class PigDiceGame {
		field RandomGenerator random;
		static int winScore;

		// two player objects, 1 for user (you) and 1 for computer
		field Player user;
		field Player computer;
		field int userScore;
		field int computerScore; 
		field int diceRoll; // the dice roll for the turn, 1 till 6
		field int rollScore; // the score for the round

		field char command; // current command, "r" for roll dice or "h" for hold to add points to score
		field int computerCount; // times that computer rolls dice in a turn
		field boolean userGetZeroPoints; // if user gets 0 points in dice roll by hitting "1";
		

		constructor PigDiceGame new() {
			// initialize
			let winScore = 100;
			let random = RandomGenerator.new(542); // initialize random generator with seed.
			let user = Player.new();
			let computer = Player.new();
			let userScore = user.getScore();
			let computerScore = computer.getScore();
			let userGetZeroPoints = false;
			let rollScore = 0;
			

			do Output.printString("Let the pig dice game begin! First to 100 points win.");
			do Output.println();

			return this;
		}

		/** Runs the game  */
		method void run() {
			// Game ends if either player achieves at least 100 points
			while((userScore < winScore) & (computerScore < winScore)) {
				// If user rolls 0 points last turn, just start computer's turn w/o messaging
				if(~userGetZeroPoints) {
					// Display scores
					do Output.printString("Your score is: ");
					do Output.printInt(userScore);
					do Output.printString(", Computer's score is: ");
					do Output.printInt(computerScore);
					do Output.println();

					// Display instructions
					do Output.printString("Hold or roll? 'h' for hold and 'r' for roll:");
					let command = Keyboard.readChar();
					do Output.moveCursor(0,0);
					do Screen.clearScreen();
				}

				// Computer's turn if user pressed "h" = 72, i.e. hold, or user rolls "1" in last turn
				if((command = 72) | userGetZeroPoints) {
					// update players score
					do Output.printString("You scored this turn: "); 
					if (~(diceRoll = 1)) {
						do Output.printInt(rollScore);
						do Output.println();
						do user.addScore(rollScore);
						let userScore = user.getScore();
					} else {
						do Output.printInt(0);
						do Output.println();
					}

					if (~(userScore < winScore)) {
						do win();
						return;
					}

					// Computer rolls 1 to 4 times
					do Output.printString("Computer's turn!");
					do Output.println();
					let computerCount = random.randRange(4) + 1;
					do user.changeRandom(computerCount);
					let userGetZeroPoints = false;

					// Roll dice until computerCount or 1 is rolled
					let diceRoll = 0;
					let rollScore = 0;
					while((computerCount > 0) & (~(diceRoll = 1))) {
						do Output.printString("Computer rolls: ");
						let diceRoll = computer.roll();
						let rollScore = rollScore + diceRoll;
						do Output.printInt(diceRoll);
						do Output.println();
						let computerCount = computerCount - 1;
					}

					// Add the points for the turn to computer's score if 1 is not hit and update computer's score
					if (~(diceRoll = 1)) {
						do Output.printString("Computer scored this turn: ");
						do computer.addScore(rollScore);
						let computerScore = computer.getScore();
						do Output.printInt(rollScore);
						do Output.println();
					} else { 
						do Output.printString("Computer rolled a 1. No points for round.");
						do Output.println();
					}

					// restart rollScore to next round
					let rollScore = 0;
				}

				// "r" = 82, i.e. roll the dice
				if (command = 82) {
					do Output.printString("You roll: ");
					let diceRoll = user.roll();
					do Output.printInt(diceRoll);
					do Output.println();

					// Update score for round if 1 is not hit
					if (~(diceRoll = 1)) {
						let rollScore = rollScore + diceRoll;
						do Output.printString("Your score for this round is: ");
						do Output.printInt(rollScore);
						do Output.println();
						do computer.changeRandom(1); // increment times that random has been called
					}
					
					// If user rolls "1", computer should start next turn
					if (diceRoll = 1) {
						let userGetZeroPoints = true;
						let command = 72; 
						do Output.printString("You roll a 1! Now it is computer's turn!");
						do Output.println();
					}
				}
			}

			// Decide who is the winner
			do win();
			return;
		}

		method void win() {
			if(~(userScore < winScore)) {
				do Output.printString("You win! With total points: ");
				do Output.printInt(userScore);
			}

			if(~(computerScore < winScore)) {
				do Output.printString("Computer wins! With total points: ");
				do Output.printInt(computerScore);
			}
			return;
		}

		method void dispose() {
			do user.dispose();
			do computer.dispose();
			return;
		}
}